plugins {
    id "org.jetbrains.intellij" version "0.4.5"
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'https://repo.spring.io/libs-snapshot/'
    }
    //maven { url 'https://jitpack.io' }
}

intellij {
    //version '2018.3'
    plugins = ['IntelliLang', 'java']
    pluginName 'idea-boot-java'
    downloadSources = false
    localPath '/opt/ideaIC-2019'
}

patchPluginXml {
    untilBuild '193.*'
    sinceBuild '183.3647.12'
}


configurations {
    lspProvider
}

dependencies {
//    implementation 'com.github.ballerina-platform:lsp4intellij:next-release-SNAPSHOT'
    implementation 'org.wso2.lsp4intellij:lsp4intellij:8.0.1-SNAPSHOT'
    compile 'org.springframework.ide.vscode:commons-java:1.11.0-SNAPSHOT'
    lspProvider ('org.springframework.ide.vscode:spring-boot-language-server:1.11.0-SNAPSHOT:exec') {
        transitive = false
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['resources']
        }
    }
}

prepareSandbox.doLast {
    def pluginServerDir = "${intellij.sandboxDirectory}/plugins/idea-boot-java/lib/server"

    mkdir(pluginServerDir)
    copy {
        from configurations.lspProvider
        into(pluginServerDir)
        rename("spring-boot-language-server.*\\.jar", "language-server.jar")
    }
}

publishPlugin.doFirst {
    def pluginServerDir = "${intellij.sandboxDirectory}/plugins/idea-boot-java/lib/server/language-server.jar"
    delete(pluginServerDir)    
}

