plugins {
    id "org.jetbrains.intellij" version "0.4.5"
    id 'com.jfrog.bintray' version '1.8.4'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'https://repo.spring.io/libs-snapshot/'
    }
    //maven { url 'https://jitpack.io' }
    maven {
        url 'https://dl.bintray.com/gayanper/maven'
    }
}

def CI_JOB = Boolean.valueOf(System.getenv("CI_JOB"))
version = new Date().format("yyyyMMdd.HH.mm.ss.SSS")

intellij {
    pluginName 'idea-boot-java'
    downloadSources = false

    if(CI_JOB) {
        version '2018.3'
        plugins = ['IntelliLang']
    } else {
        localPath '/opt/ideaIC-2019'
        plugins = ['IntelliLang', 'java']
    }
}

patchPluginXml {
    untilBuild '193.*'
    sinceBuild '183.3647.12'
}


configurations {
    lspProvider
}

dependencies {
//    implementation 'com.github.ballerina-platform:lsp4intellij:next-release-SNAPSHOT'
    implementation 'org.wso2.lsp4intellij:lsp4intellij:20190929.06.22.10.723'
    compile 'org.springframework.ide.vscode:commons-java:1.11.0-SNAPSHOT'
    lspProvider ('org.springframework.ide.vscode:spring-boot-language-server:1.11.0-SNAPSHOT:exec') {
        transitive = false
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['resources']
        }
    }
}

prepareSandbox.doLast {
    def pluginServerDir = "${intellij.sandboxDirectory}/plugins/idea-boot-java/lib/server"

    mkdir(pluginServerDir)
    copy {
        from configurations.lspProvider
        into(pluginServerDir)
        rename("spring-boot-language-server.*\\.jar", "language-server.jar")
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_API_KEY")
    publish = true
    override = true
    pkg {
        repo = 'idea-plugins'
        name = 'idea-boot-java'
        licenses = ['EPL-2.0']
        userOrg = System.getenv("BINTRAY_USER")
        vcsUrl = 'https://github.com/gayanper/sts4.git'
        version {
            name = project.version
        }
    }

    filesSpec {
        from 'build/distributions'
        into '.'
        rename 'idea-boot-java.zip', 'idea-boot-java-' + project.version + '.zip'
    }
}
